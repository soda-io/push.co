#
# –í—Å–ø–æ–º–æ–≥–∞—Ç–µ–ª—å–Ω—ã–µ —Ñ—É–Ω–∫—Ü–∏–∏
#
#

crypto    = require "crypto"
home      = process.env.HOME + "/.push.in.co"
uData     = process.env.HOME + "/.push.in.co+data"
fs        = require "fs"
_         = require "underscore"
require "colors"


#
# Public: –•–µ—à —Å—Ç—Ä–æ–∫–∏
#
#
exports.createHash = createHash = (str, secret="soda labs") ->
  crypto.createHmac("sha1", secret).update(str).digest("hex")


#
# –ù–∞—Å—Ç—Ä–æ–π–∫–∏ –ø–æ —É–º–æ–ª—á–∞–Ω–∏—é
#
_defaultSettings = ->
  user:
    name      : null
    avatar    : null
  push:
    email     : null
    phone     : null
  secretHash  : createHash "my-hash"
  dataFile    : uData
  outFormat   : "H:6|T:40"
  daysForTodo : 7


# ----------------------------------------
# –°–¢–†–û–ö–ò
# ----------------------------------------

#
# Public: –¶–µ–Ω—Ç—Ä–∏—Ä–æ–≤–∞—Ç—å —Å—Ç—Ä–æ–∫—É 
#
#
_centerString = (str, len) ->
  r = "#{str}"
  if r.length < len
    delta = parseInt (len - r.length) / 2
    "#{([1..delta].map (_x) -> ' ').join ''}#{r}"
  else
    r

#
# Public: –ò–Ω–¥–µ–∫—Å –º–µ—Å—è—Ü–∞ –≤ –∏–º—è
#
#
_getMonthName = (ind) ->
  "—è–Ω–≤–∞—Ä—å:—Ñ–µ–≤—Ä–∞–ª—å:–º–∞—Ä—Ç:–∞–ø—Ä–µ–ª—å:–º–∞–π:–∏—é–Ω—å:–∏—é–ª—å:–∞–≤–≥—É—Å—Ç:—Å–µ–Ω—Ç—è–±—Ä—å:–æ–∫—Ç—è–±—Ä—å:–Ω–æ—è–±—Ä—å:–¥–µ–∫–∞–±—Ä—å".split(":")[ind] or "???"

#
# Public: –®–∞–ø–∫–∞ –∫–∞–ª–µ–Ω–¥–∞—Ä—è
#
_getCalHead = ->
  "| –ø–Ω | –≤—Ç | —Å—Ä | —á—Ç | –ø—Ç | —Å–± | –≤—Å |"

#
# Public: –î–µ–Ω—å –Ω–µ–¥–µ–ª–∏
#
_dayOfWeek = (date) ->
  _wd = date.getDay()
  if _wd is 0
    return 6
  return _wd - 1


#
# Public: –ü–æ–ª—É—á–∏—Ç—å –¥–µ–Ω—å –Ω–µ–¥–µ–ª–∏ –ø–µ—Ä–≤–æ–≥–æ —á–∏—Å–ª–∞ –º–µ—Å—è—Ü–∞
#
_firstDayOfMonth = (date) ->
  day = new Date date
  day.setDate 1
  _dayOfWeek day
  

#
# Public: –ü–æ–ª—É—á–∏—Ç—å –ø–æ—Å–ª–µ–¥–Ω–∏–π –¥–µ–Ω—å –º–µ—Å—è—Ü–∞
#
# http://learn.javascript.ru/task/poslednij-den-mesyaca
_getMaxDay = (date) ->
  day = new Date date.getFullYear(), date.getMonth()+1, 0
  day.getDate()

#
# Public: Return calendar cell
#
#
_calendarCell = (day, is_today) ->
  day = if day > 9 then "#{day}" else " #{day}"
  day = "#{day.magenta.bold}" if is_today
  " #{day} |"

#
# Public: –û—Ç—Ä–∏—Å–æ–≤–∞—Ç—å –∫–∞–ª–µ–Ω–¥–∞—Ä—å –≤ –∫–æ–Ω—Å–æ–ª–∏
#
#
_drawCalendar = (d) ->
  cal_str = ["\n"]
  cal_str.push _centerString "#{_getMonthName d.getMonth()} #{d.getFullYear()}", 40 #
  cal_str.push _getCalHead()
  first_day = _firstDayOfMonth d
  today = d.getDate()
  s = ["|"]
  for j in [0...first_day]
    s.push _calendarCell " "
  _day = 1
  for j in [first_day...7]
    s.push _calendarCell _day, _day++ is today
  cal_str.push s.join ""

  max_day = _getMaxDay d

  while _day <= max_day
    s = ["|"]
    for j in [0...7]
      if _day <= max_day
        s.push _calendarCell _day, _day++ is today
      else
        s.push _calendarCell " "
    cal_str.push s.join ""
  cal_str.push "\n"
  console.log cal_str.join "\n"


#
# Internal: –°–∏–º–≤–æ–ª—ã –¥–ª—è –æ—Ç–æ–±—Ä–∞–∂–µ–Ω–∏—è –≤ –∫–æ–Ω—Å–æ–ª–∏
#
#
statusSymbols =
  "todo"     : symbol: "‚òê", color: "red", final: no
  "frozen"   : symbol: "‚ùÑ", color: "blue", final: no
  "question" : symbol: "¬ø", color: "yellow", final: no
  "idea"     : symbol: "‚öó", color: "magenta", final: no
  "bug"      : symbol: "‚öí", color: "red", final: no
  "done"     : symbol: "‚òë", color: "green", final: yes
  "closed"   : symbol: "‚òí", color: "grey", final: yes
  "wontfix"  : symbol: "‚öî", color: "grey", final: yes
  "fixed"    : symbol: "‚ú™", color: "green", final: yes
  "merged"   : symbol: "‚ö≠", color: "magenta", final: yes
  "pushed"   : symbol: "‚Ü¶", color: "cyan", final: yes
  "event"    : symbol: "ùçî", color: "blue", final: "event"


#
# Internal: –ù–∞—á–∞–ª—å–Ω—ã–µ –∏ –∫–æ–Ω–µ—á–Ω—ã–µ —Å–æ—Å—Ç–æ—è–Ω–∏—è
#
initialStates = []
finalStates   = []
for k,v of statusSymbols
  if v.final is yes
    finalStates.push k
  else if v.final is no
    initialStates.push k


#
# –°—á–∏—Ç–∞—Ç—å –º–∏–Ω–∏–º–∞–ª—å–Ω—ã–µ –Ω–∞—Å—Ç—Ä–æ–π–∫–∏
#
# :cf - –∫–æ–Ω—Ñ–∏–≥
#
# :fn  - –æ–±—Ä–∞—Ç–Ω—ã–π –≤—ã–∑–æ–≤
#   :err - –æ—à–∏–±–∫–∞
#   :cf  - –∏–∑–º–µ–Ω–µ–Ω–Ω—ã–π –∫–æ–Ω—Ñ–∏–≥
#
_readConfigData = (cf, fn) ->
  if cf.user.name is null
    cf.user.name = process.env.USER
  #cf.user.email ?
  fn null, cf

#
# Public: –ó–∞–≥—Ä—É–∑–∏—Ç—å —Ñ–∞–π–ª —Å –Ω–∞—Å—Ç—Ä–æ–π–∫–∞–º–∏
# :fn - –æ–±—Ä–∞—Ç–Ω—ã–π –≤—ã–∑–æ–≤
#   :err - –æ—à–∏–±–∫–∞
#   :cf  - –∫–æ–Ω—Ñ–∏–≥
#
exports.loadConfig = (fn) ->
  try
    cf = JSON.parse fs.readFileSync home, "utf-8"
    _readConfigData cf, fn
  catch e
    cf = _defaultSettings()
    _readConfigData cf, (err, cf) ->
      unless err
        fs.writeFileSync home, JSON.stringify(cf)
      fn err, cf

#
# Public: –°–æ—Ö—Ä–∞–Ω–∏—Ç—å –∫–æ–Ω—Ñ–∏–≥—É—Ä–∞—Ü–∏—é
#
exports.saveConfig = (cf) ->
  fs.writeFileSync home, JSON.stringify cf

# ----------------------------------------
# –í—ã–∑–æ–≤—ã –∫–∞—Ç–∞–ª–æ–≥–æ–≤
# ----------------------------------------

#
# Internal: –°–æ–∑–¥–∞—Ç—å –Ω–æ–≤—ã–π –∫–∞—Ç–∞–ª–æ–≥
#
_createFolder = (user_name, name, is_public, order, can_remove=yes) ->
  now = Date.now()
  hash       : createHash name
  created_at : now
  updated_at : now
  owner_name : user_name
  name       : name
  order      : order
  can_remove : can_remove
  is_public  : is_public


#
# Internal: –°–æ–∑–¥–∞—Ç—å –æ–±—ä–µ–∫—Ç —Å –∑–∞–¥–∞—á–∞–º–∏
#
_defaultDataFile = (cf) ->
  data = folders: {}, tasks: {}, end_tasks: {} # hash - folder hash, tasks in array
  now = Date.now()
  for f,i in ["personal", "family", "work"]
    f = _createFolder cf.user.name, f, no, i, no
    data.folders[f.hash] = f
    if 0 is i
      data.defaultFolder =
        hash: f.hash
        name: f.name
  data

#
# Public: –°–æ–∑–¥–∞—Ç—å –∫–∞—Ç–∞–ª–æ–≥
#
exports.createFolder = (cf, data, folder, fn=->) ->
  lastOrder = 0
  for k,v of data.folders
    if v.order > lastOrder
      lastOrder = v.order
    if v.name.toLowerCase() is folder.name.toLowerCase()
      return fn msg: "–∫–∞—Ç–∞–ª–æ–≥ —Å—É—â–µ—Å—Ç–≤—É–µ—Ç", null
  f = _createFolder cf.user.name, folder.name, folder.is_public, lastOrder+1
  data.folders[f.hash] = f
  fn null, data, f

#
# Public: –ü–µ—Ä–µ–∏–º–µ–Ω–æ–≤–∞—Ç—å –∫–∞—Ç–∞–ª–æ–≥
#
# :target  - —Ü–µ–ª–µ–≤—ã–µ –¥–∞–Ω–Ω—ã–µ
#   :old_name  - —Å—Ç–∞—Ä–æ–µ –∏–º—è –∫–∞—Ç–∞–ª–æ–≥–∞
#   :new_name  - –Ω–æ–≤–æ–µ –∏–º—è –∫–∞—Ç–∞–ª–æ–≥–∞
#
exports.renameFolder = (cf, data, target, fn=->) ->
  for k,v of data.folders
    if v.name.toLowerCase() is target.old_name.toLowerCase()
      v.name = target.new_name
      if data.defaultFolder.hash is k
        data.defaultFolder.name = v.name
      return fn null
  fn msg:"–∏—Å—Ö–æ–¥–Ω—ã–π –∫–∞—Ç–∞–ª–æ–≥ –Ω–µ –Ω–∞–π–¥–µ–Ω"


#
# Internal: –ü–æ–ª—É—á–∏—Ç—å —Ö–µ—à –∫–∞—Ç–∞–ª–æ–≥–∞ –ø–æ –∏–º–µ–Ω–∏
#
#
_getFolderHash = (name, folders) ->
  for k,v of folders
    if name.toLowerCase() is v.name.toLowerCase()
      return k
  null

#
# Public: –£–¥–∞–ª–∏—Ç—å –∫–∞—Ç–∞–ª–æ–≥
#
exports.removeFolder = (cf, data, folder, fn=->) ->
  rmByHash = (data, hash) ->
    if data.folders[hash].can_remove 
      delete data.folders[hash]
      fn null, data
    else
      fn msg: "–∫–∞—Ç–∞–ª–æ–≥ –∑–∞—â–∏—â–µ–Ω –æ—Ç —É–¥–∞–ª–µ–Ω–∏—è"

  for k,v of data.folders
    if folder.name?
      if v.name.toLowerCase() is folder.name.toLowerCase()
        return rmByHash data, k
        
    else if folder.hash?
      if 0 is v.hash.indexOf folder.hash
        return rmByHash data, k
    else
      return fn msg: "—Ö–µ—à –∏–ª–∏ –∏–º—è –Ω–µ —É–∫–∞–∑–∞–Ω—ã", null
  fn msg: "–∫–∞—Ç–∞–ª–æ–≥ –Ω–µ –Ω–∞–π–¥–µ–Ω"

# –∫–æ–Ω–µ—Ü –≤—ã–∑–æ–≤–æ–≤ –¥–ª—è –∫–∞—Ç–∞–ª–æ–≥–æ–≤
# ----------------------------------------


# ----------------------------------------
# –í—ã–∑–æ–≤—ã –¥–ª—è –∑–∞–¥–∞—á
# ----------------------------------------

#
# Internal: –ù–∞–π—Ç–∏ —à–∞–±–ª–æ–Ω –∏ —É–¥–∞–ª–∏—Ç—å –∏–∑ –∏—Å—Ö–æ–¥–Ω–æ–≥–æ –º–∞—Å—Å–∏–≤–∞
#
#
_findAndRemove = (tags, pattern) ->
  result = []
  tag = null
  for t in tags
    if pattern.test t
      tag = t
    else
      result.push t
  [tag, result]


#
# Internal: –ù–∞–π—Ç–∏ —à–∞–±–ª–æ–Ω
#
#
_find = (tags, pattern) ->
  result = []
  for t in tags
    if pattern.test t
      result.push t 
  [result, tags]

#
# Internal: –ù–∞–π—Ç–∏ —à–∞–±–ª–æ–Ω `at:12.03.12-12:30`
#
# –í–ê–†–ò–ê–ù–¢–´
# at:12.03.10-12:30:50     # —á–∏—Å–ª–æ, –º–µ—Å—è—Ü, –≥–æ–¥, —á–∞—Å, –º–∏–Ω—É—Ç–∞ –∏ —Å–µ–∫—É–Ω–¥–∞
# at:12.03.10-12:30        # —á–∏—Å–ª–æ, –º–µ—Å—è—Ü, –≥–æ–¥, —á–∞—Å –∏ –º–∏–Ω—É—Ç–∞
# at:12.03.10-12           # —á–∏—Å–ª–æ, –º–µ—Å—è—Ü, –≥–æ–¥ –∏ —á–∞—Å
# at:12.03.10              # —á–∏—Å–ª–æ, –º–µ—Å—è—Ü –∏ –≥–æ–¥
# at:12.03                 # —á–∏—Å–ª–æ –∏ –º–µ—Å—è—Ü 
# at:12                    # —á–∏—Å–ª–æ —ç—Ç–æ–≥–æ –∏–ª–∏ —Å–ª–µ–¥. –º–µ—Å—è—Ü–∞
# at:null                  # —Å–±—Ä–æ—Å at
#
#
_getAtTime = (tags) ->
  [at, tags] = _findAndRemove tags, /^at:([-\d\.\:]+|null)$/
  if at?
    at = at[3..]
    d = new Date
    # —ç—Ç–æ—Ç –∫–æ–¥ - –ø—Ä–æ—Å—Ç–æ –∑–∞–≥–ª—É—à–∫–∞
    if /^\d\d$/.test at                     # —á–∏—Å–ª–æ
      at = new Date d.getFullYear(), d.getMonth(), parseInt at
    else if /^\d\d\.\d\d$/.test at          # —á–∏—Å–ª–æ –∏ –º–µ—Å—è—Ü
      [d,m] = at.split(".").map (x) -> parseInt x
      m--
      at = new Date d.getFullYear(), m, d
    else if /^\d\d\.\d\d\.\d{2,4}$/.test at # —á–∏—Å–ª–æ, –º–µ—Å—è—Ü –∏ –≥–æ–¥
      [d, m, y] = at.split(".").map (x) -> parseInt x
      if y < 99
        y += 2000
      m--
      at = new Date y, m, d
    # else if /^\d\d\.\d\d\.\d{2,4}-\d\d$/.test at # —á–∏—Å–ª–æ, –º–µ—Å—è—Ü, –≥–æ–¥ –∏ —á–∞—Å
    #   at = ...
    # else if /^\d\d\.\d\d\.\d{2,4}-\d\d\:\d\d$/.test at # —á–∏—Å–ª–æ, –º–µ—Å—è—Ü, –≥–æ–¥, —á–∞—Å –∏ –º–∏–Ω—É—Ç–∞
    #   at = ...
    # else if /^\d\d\.\d\d\.\d{2,4}-\d\d\:\d\d\:\d\d$/.test at # —á–∏—Å–ª–æ, –º–µ—Å—è—Ü, –≥–æ–¥, —á–∞—Å, –º–∏–Ω—É—Ç–∞ –∏ —Å–µ–∫—É–Ω–¥–∞
    #   at = ...
    else
      return [null, tags, no]
    [at, tags, yes]
  else
    [null, tags, no]

#
# Internal: tags
#
#
_getRegular = (tags) ->
  [pattern, tags] = _findAndRemove tags, /^::\d\d?[mwdy]$/i
  unless pattern is null
    number = parseInt pattern[2..-2]
    scale = pattern[-1..]
    pattern = [number, scale]
  [pattern, tags]


#
# Internal: –ü–æ–ª—É—á–∏—Ç—å —É–ø–æ–º–∏–Ω–∞–Ω–∏—è
#
# –£–ø–æ–º–∏–Ω–∞–Ω–∏–µ –≤ —Ç–µ–∫—Å—Ç–µ –ø–æ –∏–º–µ–Ω–∏ —á–µ—Ä–µ–∑ @UserName
# –£–ø–æ–º–∏–Ω–∞–Ω–∏–µ –Ω–µ —É–¥–∞–ª—è–µ—Ç—Å—è
# 
_getMentions = (tags) ->
  [mentions, tags] = _find tags, /\@[-_a-z]+/ig
  [_.unique(mentions), tags]

#
# Internal: –ü–æ–ª—É—á–∏—Ç—å –ø—Ä–∏–æ—Ä–∏—Ç–µ—Ç–Ω–æ—Å—Ç—å –∑–∞–¥–∞—á–∏
#
# –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é –ø—Ä–∏–æ—Ä–∏—Ç–µ—Ç 0. –ó–∞–¥–∞–µ—Ç—Å—è –∫–∞–∫ `p:1`, `p:2`, ...
#
_getTaskPriority = (tags) ->
  [pr, tags] = _findAndRemove tags, /^p:-?\d$/i
  if pr?
    [parseInt(pr[2..]), tags, yes]
  else
    [0, tags, no]

#
# Internal: –ü–æ–ª—É—á–∏—Ç—å –∏–Ω–¥–µ–∫—Å –∑–∞–¥–∞—á–∏
#
_fetchTaskIndex = (tags) ->
  num = tags.shift()                 # num or hash
  if /^\d+$/.test num
    opts = num: parseInt num
  else
    opts = hash: num
  [opts, tags]

#
# Internal: –ü—Ä–æ–≤–µ—Ä–∏—Ç—å –µ—Å—Ç—å –ª–∏ —Å–æ–≤–ø–∞–¥–µ–Ω–∏—è —ç–ª–µ–º–µ–Ω—Ç–æ–≤
# –∏–∑ `source` –≤ `dest`
#
# :source - –∏—Å—Ö–æ–¥–Ω—ã–π –º–∞—Å—Å–∏–≤
# :dest   - –º–∞—Å—Å–∏–≤-–Ω–∞–∑–Ω–∞—á–µ–Ω–∏–µ
# :fullText - –ø–æ–ª–Ω—ã–π –ø–æ–∏—Å–∫ –ø–æ —Ç–µ–∫—Å—Ç—É
#
_matchInArray = (source, dest, fullText=no) ->
  for s in source
    if fullText
      if 0 <= dest.join(" ").toLowerCase().indexOf s.toLowerCase()
        return yes
    else if s in dest
      return yes
  no


#
# Internal: –ö–æ–º—É –¥–µ–ª–µ–≥–∏—Ä–æ–≤–∞–Ω–∞
#
#
_delegatedTo = (tags) ->
  # to:@UserName
  null

#
# Internal: –ü–æ–ª—É—á–∏—Ç—å —Å–æ—Å—Ç–æ—è–Ω–∏–µ –∑–∞–¥–∞—á–∏
#
#
_getState = (tags) ->
  [state, tags] = _findAndRemove tags, /^::[a-z\d]+$/i
  if state?
    [state[2..].toLowerCase(), tags, yes]
  else
    ["todo", tags, no]

#
# Internal: –ü–æ–ª—É—á–∏—Ç—å –∫–∞—Ç–∞–ª–æ–≥
#  –∑–∞–¥–∞–µ—Ç—Å—è –∫–∞–∫ to::work
#
#
_getFolder = (tags) ->
  [folder, tags] = _findAndRemove tags, /^to::[-a-z\.–∞-—è\d]+$/i
  if folder is null
    [null, tags]
  else
    [folder[4..], tags]

#
# Internal: –ü–æ–ª—É—á–∏—Ç—å —Ö–µ—à-—Ç–µ–≥–∏
#
#
_getHashTags = (tags, splitter="+") ->
  re = new RegExp "^\\#{splitter}[a-z–∞-—è]+[-a-z–∞-—è\d]+$", "i"
  ht = []
  new_tags = []
  for t in tags
    if re.test t
      ht.push "##{t[1..]}"
      new_tags.push "##{t[1..]}"
    else
      new_tags.push t
  [ht, new_tags]

#
# Internal: –ü–æ–ª—É—á–∏—Ç—å –∞–¥—Ä–µ—Å–∞ url
#
_getUrls = (tags) ->
  _find tags, /^(https?:\/\/)?([\da-z\.-]+)\.([a-z\.]{2,6})([\/\w \.-]*)*\/?$/

#
# Internal: initialize tags
#
_initTask = (task) ->
  now                 = Date.now()
  task.hash           = createHash task.text
  task.delegated_to ||= null
  task.created_at     = now
  task.updated_at     = now
  task.times          = []



#
# Internal: –ü—Ä–æ–≤–µ—Ä–∏—Ç—å —É–Ω–∏–∫–∞–ª—å–Ω–æ—Å—Ç—å –∑–∞–¥–∞—á–∏
#
#
_ensureUnique = (tasks, task) ->
  for t in tasks
    if t.hash is task.hash or t.text is task.text
      return no
  yes


#
# Internal: –û–±–Ω–æ–≤–∏—Ç—å –≤—Ä–µ–º—è –∏–∑–º–µ–Ω–µ–Ω–∏—è –∑–∞–¥–∞—á–∏
#
#
_touchTaskData = (task) ->
  task.updated_at        = Date.now()  

#
# Internal: –û–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –ø–æ –∑–∞–¥–∞—á–∏ –∏–∑ `tags`:
#
#   `hashtags`, `mention`, `urls`, `text`
#
_updateTaskData = (task, tags) ->
  [task.hashtags, tags]  = _getHashTags     tags
  [task.urls,     tags]  = _getUrls         tags
  [task.mention,  tags]  = _getMentions     tags
  task.text              = tags.join " "
  _touchTaskData  task


#
# Public: –î–æ–±–∞–≤–∏—Ç—å –Ω–æ–≤—É—é –∑–∞–¥–∞—á—É
#
# –Ω–æ–≤–∞—è –∑–∞–¥–∞—á–∞ –º–æ–∂–µ—Ç –±—ã—Ç—å —Å–æ–±—ã—Ç–∏–µ–º, –æ–±—ã—á–Ω–æ–π –∏–ª–∏ —Ä–µ–≥—É–ª—è—Ä–Ω–æ–π –∑–∞–¥–∞—á–µ–π
#
exports.addTask = (tags, cf, userData, fn=->) ->
  taskData =
    folder_hash: userData.defaultFolder.hash
    owner_name: cf.user.name
      
  [taskData.at,       tags]  = _getAtTime       tags
  [taskData.priority, tags]  = _getTaskPriority tags
  [taskData.regular,  tags]  = _getRegular      tags
  [taskData.state,    tags]  = _getState        tags
  [folder,            tags]  = _getFolder       tags
  unless folder is null
    folder_hash = _getFolderHash folder, userData.folders
    unless folder_hash
      return fn msg: "–∫–∞—Ç–∞–ª–æ–≥ #{folder} –Ω–µ –Ω–∞–π–¥–µ–Ω"
    taskData.folder_hash = folder_hash

  # event
  unless taskData.at is null
    taskData.state = "event"

  _updateTaskData taskData, tags
    #  todo add time_limit
  _initTask taskData

  userData.tasks[taskData.folder_hash] ||= []
  if _ensureUnique userData.tasks[taskData.folder_hash], taskData
    userData.tasks[taskData.folder_hash].unshift taskData
    # todo sort
    fn null, taskData
  else
    fn msg: "–∑–∞–¥–∞—á–∞ –¥—É–±–ª–∏—Ä—É–µ—Ç—Å—è"


#
# Internal: –ü–æ–ª—É—á–∏—Ç—å –∑–∞–¥–∞—á—É –ø–æ –∏–¥–µ–Ω—Ç–∏—Ñ–∏–∫–∞—Ç–æ—Ä—É
#
# :userData   - –¥–∞–Ω–Ω—ã–µ
# :opts
#   :num      - –ø–æ—Ä—è–¥–∫–æ–≤—ã–π –Ω–æ–º–µ—Ä        | –Ω—É–∂–Ω–æ –≤—ã–±—Ä–∞—Ç—å –ª—é–±–æ–π
#   :hash     - –Ω–∞—á–∞–ª—å–Ω—ã–µ —Ü–∏—Ñ—Ä—ã —Ö–µ—à–∞   | 
# :folderHash - —Ö–µ—à –∫–∞—Ç–∞–ª–æ–≥–∞ (–æ–ø—Ü–∏—è)
#
_getTask = (userData,  opts={}, folderHash=null) ->
  tasks = userData.tasks[folderHash or userData.defaultFolder.hash] or []
  if "number" is typeof opts.num and tasks[opts.num]?
    return [tasks[opts.num], opts.num]
  if opts.hash?
    for task,i of tasks         # todo check for hash duplicates
      if 0 is task.hash.indexOf opts.hash
        return [task, i]
  [null, null]


#
# Internal: –°–æ—Ö—Ä–∞–Ω–∏—Ç—å –∑–∞–¥–∞—á—É
#
#
_saveTask = (userData, task, taskIndex, folderHash=null) ->
  folderHash ||= userData.defaultFolder.hash
  
  if task_index? and userData.tasks[folderHash][taskIndex]?
    userData.tasks[folderHash][taskIndex] = task
  else
    console.error "task index not set"


#
# Public: –£–¥–∞–ª–∏—Ç—å –∑–∞–¥–∞—á—É
#
#
exports.removeTask = (tags, cf, userData, fn=->) ->
  [opts, tags] = _fetchTaskIndex tags
  [task, num] = _getTask userData, opts
  if task
    userData.tasks[userData.defaultFolder.hash].splice num, 1
    return fn null
  fn msg: "–∑–∞–¥–∞—á–∞ –Ω–µ –Ω–∞–π–¥–µ–Ω–∞"

#
# Public: –ü–µ—Ä–µ–º–µ—Å—Ç–∏—Ç—å –∑–∞–¥–∞—á—É
#
#
exports.moveTask = (tags, cf, userData, fn=->) ->
  [opts, tags] = _fetchTaskIndex tags
  [from, to] = tags
  if "undefined" is typeof from
    return fn msg: "–Ω–µ —É–∫–∞–∑–∞–Ω—ã –∫–∞—Ç–∞–ª–æ–≥–∏ –¥–ª—è –ø–µ—Ä–µ–º–µ—â–µ–Ω–∏—è"
  if "undefined" is typeof to
    [from, to] = [userData.defaultFolder.name, from]

  from_hash = _getFolderHash from, userData.folders
  to_hash = _getFolderHash to, userData.folders
  if null in [from_hash, to_hash]
    return fn msg: "–∫–∞—Ç–∞–ª–æ–≥ –Ω–µ –Ω–∞–π–¥–µ–Ω"

  [task, num] = _getTask userData, opts, from_hash


  if task

    userData.tasks[to_hash] ||= []
    if _ensureUnique userData.tasks[to_hash], task
      userData.tasks[to_hash].unshift task
      # todo sort
      userData.tasks[from_hash].splice num, 1
    else
      fn msg: "–∑–∞–¥–∞—á–∞ –¥—É–±–ª–∏—Ä—É–µ—Ç—Å—è"

  else
    console.log "task = #{JSON.stringify task, null, 2}"
    return fn msg: "–∑–∞–¥–∞—á–∞ –Ω–µ –Ω–∞–π–¥–µ–Ω–∞"

  fn null, task

#
# Public: –î–µ–ª–∞ –Ω–∞ —Å–µ–≥–æ–¥–Ω—è
#
exports.todaysTasks = (tags, cf, userData, fn=-> ) ->
  nowDay = new Date()
  for k,v of userData.folders
    # show folders
    foundOneTask = no
    tasks = []
    for t,i in userData.tasks[k] or []
      if t.regular   # todo fix code
        reg = t.regular.join ""
        if reg is "1d"          # every day
          d = new Date(t.updated_at).getDate()
          d2 = nowDay.getDate()
          if d isnt d2
            t.state = "todo"
            userData.tasks[k][i] = t
            storeData cf, userData


      unless t.state in finalStates
        foundOneTask = yes  
        t.index = i
        tasks.push t
    if foundOneTask
      if v.name is userData.defaultFolder.name
        console.log "\n# #{v.name}".magenta
      else
        console.log "\n# #{v.name}"
      console.log "----------------------------------------"
    printTasks tasks, daysForTodo: cf.daysForTodo or 7


#
# Public: –û–±–Ω–æ–≤–∏—Ç—å —Å–æ—Å—Ç–æ—è–Ω–∏–µ –∑–∞–¥–∞—á–∏
#
#
exports.updateTask = (tags, cf, userData, fn=-> ) ->
  return fn msg: "–Ω–µ —É–∫–∞–∑–∞–Ω—ã –ø–∞—Ä–∞–º–µ—Ç—Ä—ã" if 0 is tags.length
  [opts, tags] = _fetchTaskIndex tags
  [task, num] = _getTask userData, opts
  unless task
    return fn msg: "–∑–∞–¥–∞—á–∞ –Ω–µ –Ω–∞–π–¥–µ–Ω–∞"
  
  # task
  [pr, tags, found] = _getTaskPriority tags
  task.priority = pr if found
  [at, tags, found] = _getAtTime tags
  task.at = at if found
  [state, tags, found] = _getState tags
  if found
    task.state = state # todo move to ended tasks

  if tags.length > 0
    _updateTaskData task, tags


  fn null, task


#
# Public: –ü–µ—Ä–µ–≤–µ—Å—Ç–∏ –∑–∞–¥–∞—á—É –≤ —Å–æ–±—ã—Ç–∏–µ
#
# `s tte 7 at:12.06.2014`
#
exports.toEvent = (tags, cf, userData, fn=-> ) ->
  return fn msg: "—É–∫–∞–∂–∏—Ç–µ –∑–∞–¥–∞—á—É" if 0 is tags.length
  [opts, tags] = _fetchTaskIndex tags
  [task, num] = _getTask userData, opts
  return fn msg: "–∑–∞–¥–∞—á–∞ –Ω–µ –Ω–∞–π–¥–µ–Ω–∞" if null is task
  [task.at, tags]  = _getAtTime tags
  if task.at
    task.state = "event"
    _touchTaskData task
    fn null, task
  else
    fn msg: "–¥–∞—Ç–∞/–≤—Ä–µ–º—è –Ω–µ —É–∫–∞–∑–∞–Ω—ã"


#
# Public: –ü–µ—Ä–µ–≤–µ—Å—Ç–∏ —Å–æ–±—ã—Ç–∏–µ –≤ –∑–∞–¥–∞—á—É 
#
# `s ett 8`
# 
exports.toTask = (tags, cf, userData, fn=-> ) ->
  return fn msg: "—É–∫–∞–∂–∏—Ç–µ –∑–∞–¥–∞—á—É" if 0 is tags.length
  [opts, tags] = _fetchTaskIndex tags
  [task, num] = _getTask userData, opts
  return fn msg: "–∑–∞–¥–∞—á–∞ –Ω–µ –Ω–∞–π–¥–µ–Ω–∞" if null is task
  task.state = "todo"
  task.at = null
  _touchTaskData task
  fn null, task

#
# Public: –ü–æ–∫–∞–∑–∞—Ç—å —Å–≤–æ–π—Å—Ç–≤–∞ –∑–∞–¥–∞—á–∏
#
exports.inspectTask = (tags, cf, userData, fn=->) ->
  return fn msg: "—É–∫–∞–∂–∏—Ç–µ –∑–∞–¥–∞—á—É" if 0 is tags.length
  [opts, tags] = _fetchTaskIndex tags
  [task, num] = _getTask userData, opts
  if task
    console.log JSON.stringify task, null, 2
    fn null, task
  else
    fn msg: "–∑–∞–¥–∞—á–∞ –Ω–µ –Ω–∞–π–¥–µ–Ω–∞"

#
# Public: –í—ã–≤–µ—Å—Ç–∏ –∫–∞–ª–µ–Ω–¥–∞—Ä—å
#
#
exports.showCalendar = (tags, cf, userData, fn=->) ->
  d = new Date 2014, 2, 5
  d = new Date 
  if tags.length is 0
    _drawCalendar new Date
  else
    if /^\d\d?$/.test tags[0]   # month
      d = new Date
      d.setMonth parseInt(tags[0]) - 1
      _drawCalendar d
#
# Public: –ü–æ–∫–∞–∑–∞—Ç—å —Å–ø–∏—Å–æ–∫ –∑–∞–¥–∞—á
#
exports.listTasks = (tags, cf, userData, fn=->) ->
  tasks = userData.tasks[userData.defaultFolder.hash] or []
  #maxVal = if "-a" in [tags] then 1000000 else 20
  search = null
  for t in tags
    if /^::[a-z]+$/i.test t
      search ||={}
      search.states ||= []
      search.states.push t.toLowerCase()[2..]
    else if /^\+[a-z–∞-—è]+[-a-z–∞-—è\d]+$/i.test t
      search ||={}
      search.hashtags ||= []
      search.hashtags.push "##{t[1..]}"
    else                         # filter options
      search ||={}
      search.words ||= []
      search.words.push t      


  _tasks = []
  for t,i in tasks
    if search is null and i < 20 and not (t.state in finalStates)
      t.index = i
      _tasks.push t
    else
      try
        if search.states? and  t.state in search.states
          printTask t, index: i
        else if search.hashtags? and _matchInArray search.hashtags, t.hashtags
          printTask t, index: i, words: [[search.hashtags, "red"]]   # –ø–æ–º–µ—Ç–∏—Ç—å —Å–æ–≤–ø–∞–¥–µ–Ω–∏–µ
        else if search.words? and _matchInArray search.words, t.text.split(" "), yes
          printTask t, index: i, words: [[search.words, "red"]]   # –ø–æ–º–µ—Ç–∏—Ç—å —Å–æ–≤–ø–∞–¥–µ–Ω–∏–µ
      catch e
        "skip this step"

  printTasks _tasks, daysForTodo: cf.daysForTodo or 7

#
# Internal: –ü–æ–¥—Å–≤–µ—Ç–∏—Ç—å —Ç–µ–∫—Å—Ç
#
# :text - –∏—Å—Ö–æ–¥–Ω—ã–π —Ç–µ–∫—Å—Ç
# :words[] - –º–∞—Å—Å–∏–≤ —Å–æ–≤–ø–∞–¥–µ–Ω–∏–π, —ç–ª–µ–º–µ–Ω—Ç—ã - —Å–ª–æ–≤–∞ –∏ —Ü–≤–µ—Ç :  [words, color]
#
_colorizeText = (text, words=[]) ->
  result = []
  for word in text.split " "
    found = no
    for words_array in words
      break if found
      for w in words_array[0] or []
        ind = word.toLowerCase().indexOf w.toLowerCase()
        if 0 <= ind

          wrd = word.substring ind, ind+w.length
          wrd = w.bold[words_array[1]]
          if 0 is ind
            result.push "#{wrd}#{word.substring w.length}"
          else
            _word = "#{word.substring 0, ind}#{wrd}#{word.substring ind + w.length}"
            result.push _word
          found = yes
          break
    result.push word unless found      
  result.join " "


#
# Public: –í—ã–≤–µ—Å—Ç–∏ –∑–∞–¥–∞—á–∏ –≤ –∫–æ–Ω—Å–æ–ª—å
#
printTasks = (tasks, opts={}) ->
  tasksDict = {}
  names = []
  now = Date.now()
  for t in tasks                # make key, then sort
    name = "#{-t.priority}#{now - t.updated_at}"
    tasksDict[name] = t
    names.push name
  names.sort()

  for name,i in names
    t = tasksDict[name]
    opts.index = t.index
    printTask t, opts

#
# Public: –í—ã–≤–µ—Å—Ç–∏ –∑–∞–¥–∞—á—É –≤ –∫–æ–Ω—Å–æ–ª—å
#
exports.printTask = printTask = (task, opts={}) ->
  r = []
  flags = {}
  if task.priority > 0
    flags.p = "!"
    if task.priority > 2
      flags.p = "!".red
  else
    flags.p = " "
  if statusSymbols[task.state]?
    r.push "#{statusSymbols[task.state].symbol[statusSymbols[task.state].color]} "
  else
    r.push "‚òâ "
  r.push flags.p
  unless "undefined" is typeof opts.index
    r.push "#{opts.index}\t"
  else
    r.push " \t"

  if task.state is "event"
    lastField = r.pop()
    r.push lastField.replace "\t", "  "
    d = new Date task.at
    r.push "#{d.getDate()}.0#{d.getMonth()+1}.#{d.getFullYear().toString()[2..]}\t".blue
  else if task.regular          # regular task
    r.push "#{task.regular[0]}#{task.regular[1]}\t".yellow
  else
    # days for todo
    if opts.daysForTodo?
      days = parseInt (Date.now() - task.updated_at)/ 86400000
      if days < .4 * opts.daysForTodo
        r.push "\t"
      else if days < 0.9 * opts.daysForTodo
        r.push "#{days.toString().yellow}\t"
      else
        r.push "#{days.toString().red}\t"

  # color opts
  opts.words ||= []
  opts.words.unshift [task.hashtags, "magenta"] # add urls too?
  r.push _colorizeText task.text, opts.words
  console.log r.join ""



# –∫–æ–Ω–µ—Ü –≤—ã–∑–æ–≤–æ–≤ –¥–ª—è –∑–∞–¥–∞—á
# ----------------------------------------


#
# Public: –°–æ—Ö—Ä–∞–Ω–∏—Ç—å –¥–∞–Ω–Ω—ã–µ
#
exports.storeData = (cf, data) ->
  fs.writeFileSync cf.dataFile, JSON.stringify data, null, 2

#
# Public: –ó–∞–≥—Ä—É–∑–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –∏–∑ —Ñ–∞–π–ª–∞ 
#
exports.loadData = (cf, fn) ->
  try
    userData = JSON.parse fs.readFileSync cf.dataFile, "utf-8"
  catch e
    # —Å–æ–∑–¥–∞—Ç—å –Ω–æ–≤—ã–π —Ñ–∞–π–ª
    userData = _defaultDataFile cf
    fs.writeFileSync cf.dataFile, JSON.stringify userData, null, 2
  fn null, userData
